#created on: Aug 31, 2009
package edu.ucsc.eis.mario

#list any import classes here.
import edu.ucsc.eis.mario.sprites.*
import edu.ucsc.eis.mario.events.*
import edu.ucsc.eis.mario.repairs.*
import edu.ucsc.eis.mario.level.Pit
import java.util.ArrayList
import java.util.Iterator
import java.util.Collections
import javax.jms.*
import function org.mockito.Mockito.*
import org.apache.log4j.Logger

#declare any global variables here
global javax.jms.MessageProducer producer
global javax.jms.Session session
global org.apache.log4j.Logger logger

declare Jump
    @role(event)
    @expires(5s)
end

declare Landing
    @role(event)
    @expires(1s)
end

declare BulletBillSpawn
    @role(event)
    @timestamp(spawnTime)
    @expires(3s)
end

# Test rules to make sure facts are inserted
rule "jumpEventFound"
    ruleflow-group "Test"
    when
        $jump : Jump()
    then
        logger.debug("Found a jump event: " + $jump);
end

rule "landingEventFound"
    ruleflow-group "Test"
    when
        $landing : Landing()
    then
        logger.debug("Found a landing event: " + $landing);
end

rule "billSpawnEventFound"
    ruleflow-group "Test"
    when
        $bill : BulletBillSpawn()
    then
        logger.debug("Found a Bullet Bill: " + $bill);
end

rule "valueChangeFound"
    ruleflow-group "Test"
    when
        $change : ValueChange()
    then
        logger.debug("Found a value change: " + $change);
end

# Book-keeping rules to ensure working agenda is valid from tick-to-tick
rule "Retract all jumps when a landing is found"
    ruleflow-group "Bookkeeping"
    when
        $landing : Landing()
        $jumps : ArrayList(size > 0) from collect(Jump())
    then
        for (Object jump : $jumps) {
            retract(jump);
        }
        
        retract($landing);
end

rule "Retract all landings"
    ruleflow-group "Bookkeeping"
    salience -1
    when
        $landing : Landing()
    then
        retract($landing);
end

//The window of time Mario jumped is too long
rule "marioJumpTooLong"
    ruleflow-group "Integrity"
    duration(2s)
    when
        $jump : Jump($mario : mario)
        not(Landing(this after[0s,2s] $jump))
    then
        logger.info("Mario jumped too long");
        
        try {
            producer.send(session.createObjectMessage(new MarioMovement(false, null, null)));
        } catch (Exception e) {
            if (producer == null) { System.err.println("Producer was null"); }
            if (session == null) { System.err.println("Session was null"); }
            logger.error("Jump too long exception " + e);
            e.printStackTrace();
        }
end

rule "marioDoubleJump"
    ruleflow-group "Integrity"
    when
        $jump1 : Jump()
        $jump2 : Jump(this != $jump1, this after $jump1,
            mario.isSliding == false, mario.jumpTime != 0, $mario : mario)
        //$mario : Mario(sliding == false, jumpTime != 0)
    then
        $mario.setJumpTime(-1);
        $mario.setYJumpSpeed(0);
        retract($jump2);
        logger.info("Mario double jumped");
end

// Find all Bullet Bills spawned from the same cannon in the last 10 seconds
rule "bulletBillSpawn"
    ruleflow-group "Integrity"
    when
        BulletBillSpawn($cannonId : cannonId)
        $list : ArrayList(size > 1) from collect(BulletBillSpawn(cannonId == $cannonId) over window:time(3s))
    then
        BulletBillSpawn newestBillSpawn = (BulletBillSpawn) Collections.max($list);
        LevelScene levelScene = newestBillSpawn.getBulletBill().getWorld();
        levelScene.removeSprite(newestBillSpawn.getBulletBill());
        retract(newestBillSpawn);
        logger.info("Bullet bills firing too fast, retracted " + newestBillSpawn);
end

rule "coinValue"
    ruleflow-group "Integrity"
    when
        $change : ValueChange(changeType == ValueChange.COIN_CHANGE, eval((startValue + 1) != endValue))
    then
        Mario.coins = $change.getStartValue() + 1;
        retract($change);
end

 
